#------------------------------------------------------------------------------------------------------
#
# boot.ini identification
#
#------------------------------------------------------------------------------------------------------
ODROIDC2-UBOOT-CONFIG

#------------------------------------------------------------------------------------------------------
#
# Boot Specific Stuff
#
#------------------------------------------------------------------------------------------------------
setenv bootrootfs "boot=/dev/mmcblk0p1 disk=/dev/mmcblk0p2"

#------------------------------------------------------------------------------------------------------
#
# Console Setup
#   setenv condev "console=ttyS0,115200n8"                # on serial port
#   setenv condev "console=tty0"                          # on display (HDMI)
#   setenv condev "console=tty0 console=ttyS0,115200n8"   # on both
#
#------------------------------------------------------------------------------------------------------
setenv console "console=tty0"

#------------------------------------------------------------------------------------------------------
#
# Video
#   Setup the video output
#     480 Lines (720x480)
#       setenv video_output "480i60hz"       # Interlaced 60Hz
#       setenv video_output "480i_rpt"       # Interlaced for Rear Projection Televisions 60Hz
#       setenv video_output "480p60hz"       # 480 Progressive 60Hz
#       setenv video_output "480p_rpt"       # 480 Progressive for Rear Projection Televisions 60Hz
#     576 Lines (720x576)
#       setenv video_output "576i50hz"       # Interlaced 50Hz
#       setenv video_output "576i_rpt"       # Interlaced for Rear Projection Televisions 50Hz
#       setenv video_output "576p50hz"       # Progressive 50Hz
#       setenv video_output "576p_rpt"       # Progressive for Rear Projection Televisions 50Hz
#     720 Lines (1280x720)
#       setenv video_output "720p50hz"       # Progressive 50Hz
#       setenv video_output "720p60hz"       # Progressive 60Hz
#     1080 Lines (1920x1080)
#       setenv video_output "1080i60hz"      # Interlaced 60Hz
#       setenv video_output "1080p60hz"      # Progressive 60Hz
#       setenv video_output "1080i50hz"      # Interlaced 50Hz
#       setenv video_output "1080p50hz"      # Progressive 50Hz
#       setenv video_output "1080p24hz"      # Progressive 24Hz
#     4K (3840x2160)
#       setenv video_output "2160p30hz"      # Progressive 30Hz
#       setenv video_output "2160p25hz"      # Progressive 25Hz
#       setenv video_output "2160p24hz"      # Progressive 24Hz
#       setenv video_output "smpte24hz"      # Progressive 24Hz SMPTE
#       setenv video_output "2160p50hz"      # Progressive 50Hz
#       setenv video_output "2160p60hz"      # Progressive 60Hz
#       setenv video_output "2160p50hz420"   # Progressive 50Hz with YCbCr 4:2:0 (Requires TV/Monitor that supports it)
#       setenv video_output "2160p60hz420"   # Progressive 60Hz with YCbCr 4:2:0 (Requires TV/Monitor that supports it)
#     VESA modes
#       setenv video_output "640x480p60hz"
#       setenv video_output "800x480p60hz"
#       setenv video_output "800x600p60hz"
#       setenv video_output "1024x600p60hz"
#       setenv video_output "1024x768p60hz"
#       setenv video_output "1280x800p60hz"
#       setenv video_output "1280x1024p60hz"
#       setenv video_output "1360x768p60hz"
#       setenv video_output "1366x768p60hz"
#       setenv video_output "1440x900p60hz"
#       setenv video_output "1600x900p60hz"
#       setenv video_output "1680x1050p60hz"
#       setenv video_output "1920x1200p60hz"
#
#  Setup the HDMI video mode (Must be one or the other)
#     setenv video_mode "dvi"              # DVI
#     setenv video_mode "vga"              # VGA
#
#  Setup the HDMI BPP Mode
#     setenv video_bpp "32"                # 32-bit
#     setenv video_bpp "24"                # 24-bit
#     setenv video_bpp "16"                # 16-bit
#
#  Enable|Disable HDMI Hotplug Detection
#     setenv hdmihotplug "0"               # Disabled
#     setenv hdmihotplug "1"               # Enabled
#
#  Enable|Disable HDMI (Saves RAM)
#     setenv hdmioutput "0"                # Disabled
#     setenv hdmioutput "1"                # Enabled
#
#  Enable|Disable VPU (Saves RAM)
#     setenv vpu "0"                       # Disabled
#     setenv vpu "1"                       # Enabled
#
#------------------------------------------------------------------------------------------------------
setenv video_output "1080p60hz"
setenv video_mode   "hdmi"
setenv video_bpp    "32"
setenv hdmihotplug  "1"
setenv hdmioutput   "1"
setenv vpu          "1"

#------------------------------------------------------------------------------------------------------
#
# LibreELEC variables
#
# Setup the LibreELEC options
#   valid values are: textmode debugging progress nofsck nosplash noram overlay quiet ssh
#------------------------------------------------------------------------------------------------------
setenv libreelec "quiet"

#------------------------------------------------------------------------------------------------------
#
# UHS Card Configuration
#   Uncomment the line below to __DISABLE__ UHS-1 MicroSD support
#   This might break boot for some brand models of cards.
#
#------------------------------------------------------------------------------------------------------
# setenv disableuhs "disableuhs"

#------------------------------------------------------------------------------------------------------
#
# Enable|Disable HDMI CEC Control
#   setenv hdmi_cec  "0"                  # Disabled
#   setenv hdmi_cec  "1"                  # Enabled
#
#------------------------------------------------------------------------------------------------------
setenv hdmi_cec "1"

#------------------------------------------------------------------------------------------------------
#
# PCM5102 I2S Audio DAC (HiFi Shield)
# PCM5102 is an I2S Audio DAC Addon board for ODROID-C1+
#
# Enable|Disable Audio DAC
#   setenv audio_dac "0"                  # Disabled
#   setenv audio_dac "1"                  # Enabled
#------------------------------------------------------------------------------------------------------
setenv audio_dac "0"

#------------------------------------------------------------------------------------------------------
#
# Odroid C1 specific
#
#------------------------------------------------------------------------------------------------------
setenv odroidp1 "no_console_suspend hdmimode=${video_output} m_bpp=${video_bpp} vout=${video_mode}"
setenv odroidp2 "${disableuhs} consoleblank=0"
setenv odroid   "${odroidp1} ${odroidp2}"

#------------------------------------------------------------------------------------------------------
#
# U-Boot bootcmd command
#
#------------------------------------------------------------------------------------------------------
setenv kernel  "fatload mmc 0:1 0x11000000 KERNEL"
setenv dtb     "fatload mmc 0:1 0x1000000  meson64_odroidc2.dtb"
setenv bootseq "booti 0x11000000 - 0x1000000"
setenv timer   "fdt addr 0x1000000; fdt rm /timer"
setenv bootcmd "${kernel}; ${dtb}; ${timer}; ${bootseq}"

#------------------------------------------------------------------------------------------------------
#
# Kernel boot
#
#------------------------------------------------------------------------------------------------------

# Prepare to boot
if test "${hdmi_cec}" = "1"; then setenv cec "hdmitx=cecf"; fi
if test "${hdmi_hotplug}" = "0"; then setenv hpd "disablehpd=true"; fi
if test "${audio_dac}" = "1"; then setenv dac "enabledac"; fi
if test "${vpu}" = "0"; then fdt rm /mesonstream; fdt rm /vdec; fdt rm /ppmgr; fi
if test "${hdmioutput}" = "0"; then fdt rm /mesonfb; fi

# Populate the final bootcmd
setenv bootargs "${console} ${bootrootfs} ${odroid} ${cec} ${hpd} ${dac} ${libreelec}"

# Boot the board
run bootcmd
